plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    // Firebase
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "net.xzos.upgradeall"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 86
        versionName "0.10.0-alpha.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
                arg("room.incremental", "true")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [enableCrashReporting: "true", appName: "UpgradeAll"]
        }
        debug {
            minifyEnabled false
            manifestPlaceholders = [enableCrashReporting: "false", appName: "UpgradeAll-β"]
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs = ["-Xopt-in=kotlin.RequiresOptIn", "-XXLanguage:+InlineClasses"]
    }

    buildFeatures {
        // 视图绑定
        dataBinding true
        viewBinding true
    }

    lintOptions {
        abortOnError true
        checkReleaseBuilds false
    }

    packagingOptions {
        exclude 'META-INF/**'
        exclude 'kotlin/**'
        exclude 'org/**'
        exclude '**.properties'
        exclude '**.bin'
    }

    dependenciesInfo.includeInApk false

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.drawerlayout:drawerlayout:1.1.1"
    implementation 'androidx.viewpager2:viewpager2:1.1.0-beta01'

    implementation 'androidx.fragment:fragment-ktx:1.3.6'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    // Kotlin
    implementation 'androidx.core:core-ktx:1.6.0'
    //noinspection DifferentStdlibGradleVersion
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1-native-mt'

    // WorkManager
    def work_version = '2.7.0-alpha05'
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Localization
    implementation 'com.akexorcist:localization:1.2.10'

    // Firebase
    implementation 'com.google.firebase:firebase-perf:20.0.2'
    implementation 'com.google.firebase:firebase-analytics:19.0.1'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.1'

    // 数据处理
    implementation 'com.jonathanfinerty.once:once:1.3.1'

    // 图片加载
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // 界面设计
    // Google MD 库
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.github.kobakei:MaterialFabSpeedDial:master-SNAPSHOT'
    // svg 单个 path 颜色切换
    implementation 'com.github.devsideal:VectorChildFinder:1.0.0'
    implementation 'dev.rikka.rikkax.insets:insets:1.1.0'
    implementation 'dev.rikka.rikkax.layoutinflater:layoutinflater:1.0.1'

    //RecyclerView
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.6'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation project(path: ':app-backup')
    implementation project(path: ':core-android-utils')
    implementation project(path: ':core')
    implementation project(path: ':core-utils')
    implementation project(path: ':core-downloader')
    implementation project(path: ':core-installer')
}

def optimizeReleaseRes = task('optimizeReleaseRes').doLast {
    def aapt2 = Paths.get(project.android.sdkDirectory.path, 'build-tools', project.android.buildToolsVersion, 'aapt2')
    def zip = Paths.get(
            project.buildDir.path,
            "intermediates",
            "optimized_processed_res",
            "release",
            "resources-release-optimize.ap_"
    )
    def optimized = new File("${zip}.opt")
    def cmd = exec {
        commandLine aapt2, 'optimize', '--collapse-resource-names',
                '-o', optimized, zip
        ignoreExitValue false
    }
    if (cmd.exitValue == 0) {
        delete(zip)
        optimized.renameTo("$zip")
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'optimizeReleaseResources') {
        task.finalizedBy optimizeReleaseRes
    }
}

