plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "org.mozilla.rust-android-gradle.rust-android" version "0.9.3"
}

android {
    namespace 'net.xzos.upgradeall.getter'
    compileSdk 34
    ndkVersion "26.1.10909125"

    defaultConfig {
        minSdk 23

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            cargo {
                module = "./src/main/rust/api_proxy"
                targets = ["x86_64", "arm64"]
                libname = "api_proxy"
                extraCargoBuildArguments = ["--release"]

                features {
                    all()
                }
            }
        }

        debug {
            cargo {
                module = "./src/main/rust/api_proxy"
                targets = ["x86_64", "arm64"]
                libname = "api_proxy"

                features {
                    all()
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

tasks.matching { it.name.matches(/merge.*JniLibFolders/) }.configureEach {
    it.inputs.dir(new File(buildDir, "rustJniLibs/android"))
    it.dependsOn("cargoBuild")
}